plugins {
    id "com.github.johnrengelman.shadow" version "6.1.0"
}

repositories {
    mavenCentral()
    maven {
        name = "minecraft"
        url = "https://libraries.minecraft.net"
    }
    maven {
        name = "spongepowered"
        url = "https://repo.spongepowered.org/maven"
    }
}

final String patchedFileName = "patched_1.16.5.jar"

dependencies {
    compileOnly fileTree(dir: "../libs/", include: patchedFileName)
    implementation "org.ow2.asm:asm-tree:9.2"
    implementation "org.ow2.asm:asm-commons:9.2"
    implementation "org.ow2.asm:asm-util:9.2"
    implementation "org.spongepowered:mixin:0.8.2"
    implementation "org.apache.logging.log4j:log4j-api:2.14.1"
    implementation "org.apache.logging.log4j:log4j-core:2.14.1"
    compileOnly "net.minecraft:launchwrapper:1.12"
}

jar {
    manifest {
        attributes(
                "Main-Class": "${this.getGroup()}.papermixin.Main",
                "MixinConfigs": "papermixin.mixins.json"
        )
    }
}

shadowJar {
    exclude "META-INF/*"
    archiveFileName = "server.jar"
}
tasks.build.dependsOn tasks.shadowJar

task copyBuildToServer(group: "copy", type: Copy) {
    mustRunAfter shadowJar
    from new File(buildDir.absolutePath, "/libs/server.jar")
    into "../server"
}

task copyMixinLibToServer(group: "copy", type: Copy) {
    from configurations.compileClasspath.getFiles().stream()
            .filter(x -> x.getName().matches(".*mixin-.*.jar"))
            .findFirst().get()
    into "../server"
}

task copyMixinLibToLibsDir(group: "copy", type: Copy) {
    from configurations.compileClasspath.getFiles().stream()
            .filter(x -> x.getName().matches(".*mixin-.*.jar"))
            .findFirst().get()
    into "../libs"
}

task copyPatchedToServer(group: "copy", type: Copy) {
    from new File("../libs/" + patchedFileName)
    into "../server/cache"
}

task buildServer(group: "build") {
    dependsOn shadowJar, copyBuildToServer, copyMixinLibToServer, copyPatchedToServer
}
